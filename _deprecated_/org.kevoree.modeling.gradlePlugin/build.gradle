apply plugin: 'java'
apply plugin: 'maven'


def getMavenSettingsCredentials = {
    String userHome = System.getProperty( "user.home" );
    File mavenSettings = new File(userHome, ".m2/settings.xml")
    def xmlSlurper = new XmlSlurper()
    def output = xmlSlurper.parse(mavenSettings)
    return output."servers"."server"
}

def getCredentials = {
    def entries = getMavenSettingsCredentials()
    for (entry in entries) {
        if ( entry."id".text() == "maven2.kevoree.release" ) {
            return [username: entry.username.text(), password: entry.password.text()]
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
version = '1.0-SNAPSHOT'
group = 'org.kevoree.modeling'

dependencies {
   testCompile 'junit:junit:4.11'
   compile gradleApi()
}
uploadArchives {
    def creds = getCredentials()
    repositories.mavenDeployer {
        repository(url: "http://maven.kevoree.org/archiva/repository/release/") {
            authentication(userName: creds["username"], password: creds["password"])
        }
        snapshotRepository(url: "http://maven.kevoree.org/archiva/repository/snapshots/") {
            authentication(userName: creds["username"], password: creds["password"])
        }
    }
}


